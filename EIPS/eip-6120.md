---
eip: 6120
title: Universal Token Router
description: A single router contract enables tokens to be sent to application contracts in the transfer-and-call manner instead of approve-then-call.
author: Zergity (@Zergity), Zergity <zergity@gmail.com>
discussions-to: https://ethereum-magicians.org/t/eip-6120-universal-token-router/12142
status: Review
type: Standards Track
category: ERC
created: 2022-12-12
requires: 20, 721, 1155
---

## Abstract

ETH is designed with transfer-and-call as the default behavior in a transaction. Unfortunately, [EIP-20](./eip-20.md) is not designed with that pattern in mind and newer standards are too late to replace it as the de facto standard.

Application and router contracts have to use the approve-then-call pattern which costs additional `n*m*l` `allow` (or `permit`) transactions, for `n` contracts, `m` tokens, and `l` user addresses. These allowance transactions not only cost enormous amounts of user gas, waste network storage and throughput, and worsen user experience, but also put users at serious security risks as they often have to approve unaudited, unverified and upgradable proxy contracts.

The Universal Token Router (UTR) separates the token allowance from the application logic, allowing any token to be spent in a contract call the same way with ETH, without approving any other application contracts.

Tokens approved to the Universal Token Router can only be spent in transactions directly signed by their owner, and they have clearly visible token transfer behavior, including token types (ETH, [EIP-20](./eip-20.md), [EIP-721](./eip-721.md) or [EIP-1155](./eip-1155.md)), `amountInMax`, `amountOutMin`, and `recipient`.

The Universal Token Router contract is counter-factually deployed using `CREATE2` at a single address across all EVM-compatible networks, so new token contracts can pre-configure it as a trusted spender and no approval transaction is necessary ever again.

## Motivation

When users approve their tokens to a contract, they trust that:

* it only spends the tokens with their permission (usually from `msg.sender` or using `ecrecover`)
* it does not use `delegatecall` (mostly in upgradable proxies)

By performing the same security conditions above, the Universal Token Router can be shared by all applications, saving `(n-1)*m*l` approval transactions for old tokens and **ALL** approval transactions for new tokens.

Before this EIP, when users sign transactions to spend their approved tokens, they trust the front-end code entirely to construct those transactions honestly and correctly. This puts them at great risk of phishing sites.

The Universal Token Router function arguments can act as a manifest for users when signing a transaction. With the support from wallets, users can see and review their expected token behavior instead of blindly trusting the application contracts and front-end code. Phishing sites will be much easier to detect and avoid for users.

Application contracts follow this standard can use the Universal Token Router to have the following benefits:

* Safely share the user token allowance with all other applications.
* Freely update their helper contract logic.
* Save development and security audit costs on router contracts.

The Universal Token Router promotes the **security-by-result** model in decentralized applications instead of **security-by-process**. By directly querying token balance change for output verification, user transactions can be secured even while interacting with erroneous or malicious contracts. With non-token results, application helper contracts can provide additional result-checking functions for UTR's output action.

## Specification

```solidity
struct Token {
    uint eip;       // token standard: 0 for ETH or EIP number
    address adr;    // token contract address
    uint id;        // token id for EIP721 and EIP1155
    uint amount;    // amountInMax for input action, amountOutMin for output action
    uint offset;    // with input actions: byte offset to get the amountIn from the lastInputResult bytes
                    // with output actions: 0 for token balance change verification, or output token transfer
    address recipient;
}
```

```solidity
struct Action {
    uint output;    // 0 for input, 1 for mandatory output, 2 for optional (failable) output
    address code;   // contract code address
    bytes data;     // contract input data
    Token[] tokens; // tokens to transfer or verify balance
}
```

```solidity
interface IUniversalTokenRouter {
    function exec(Action[] calldata actions) external payable;
}
```

### Input Action

Actions with `action.output == 0` declare which and how many tokens are transferred from `msg.sender` to `token.recipient`.

1. If the `action.data` is not empty, `action.code.call(action.data)` is executed and the value returned is recorded in `lastInputResult` as a `bytes` for subsequence token transfer amounts.
2. For each `token` in `action.tokens`:
   * (a) The amount of token to be transferred is determined as:
     * if `offset < 32`, `amountIn = token.amount`
     * if `offset >= 32`, `amountIn = lastInputResult.slice(offset-32, offset)`
     * `amountIn` MUST NOT be greater than `token.amount`, otherwise, the transaction will be reverted with `EXCESSIVE_INPUT_AMOUNT`.
   * (b) If the token is `ETH` and the `recipient` is `0x0`, the next step **(c)** is skipped and the `amountIn` will be passed to the next output action as the transaction value.
   * (c) Transfer `amountIn` of token from `msg.sender` to `recipient`.

Note: `lastInputResult` is the last value returned by an input action's code contract call. It can be shared by multiple input actions until another action's code is executed. Using `lastInputResult` before any input action execution can produce unexpected `amountIn` values.

### Output Action

Actions with `action.output > 0` declare the main application action to execute, and optionally transfer and verify the output tokens after that.

1. If the `action.data` is not empty, execute the `action.code.call{value: value}(action.data)`, where:
   * `value` can be zero or the `amountIn` of the last `ETH` input with `recipient == 0x0` (see Input Action 2b),
   * any failure of the output execution will be ignored if the `action.output == 2`
2. For each `token` in `action.tokens`:
   * if `token.offset > 0`, transfer the following `amountOut` of token from `this` (UTR contract) to `token.recipient`:
     * if `token.offset >= 32`, `amountOut = lastInputResult.slice(offset-32, offset)`
     * if `token.offset == 1`:
       * if `token.amount > 0`, `amountOut = token.amount`,
       * if `token.amount == 0`, `amountOut` is the current token balance owned by `this` (UTR contract).
   * if `token.offset == 0`, verify the balance change of `token.recipient`. The balance change MUST NOT be less than `token.amount` of each token, otherwise, the transaction will be reverted with `INSUFFICIENT_OUTPUT_AMOUNT`.

The last `lastInputResult` bytes can be passed to an output action code execution by:

* using a function with the last param as a `bytes` type.
* pass the following value to that last param in the output `action.data`:

```
LAST_INPUT_RESULT = keccak256('UniversalTokenRouter.LAST_INPUT_RESULT')
```

### Output Token Verification

At the very beginning of the `exec` function, for all output actions, each token with `token.offset == 0` has its balance of `token.recipient` recorded for later verification. After each output action, those token balances are queried again for comparison.

A special id `EIP_721_ALL` is reserved for EIP-721, which can be used in output actions to verify the total amount of all ids owned by the `recipient` address.

```
EIP_721_ALL = keccak256('UniversalTokenRouter.EIP_721_ALL')
```

### Usage Samples

#### `UniswapRouter.swapExactTokensForTokens`

Legacy function:

```solidity
UniswapV2Router01.swapExactTokensForTokens(
    uint amountIn,
    uint amountOutMin,
    address[] calldata path,
    address to,
    uint deadline
)
```

This function does what `UniswapV2Router01.swapExactTokensForTokens` does, without the token transfer part:

```solidity
UniswapV2Helper01.swapExactTokensForTokens(
    uint amountIn,
    uint amountOutMin,
    address[] calldata path,
    address to,
    uint deadline
)
```

This transaction is signed by users to execute the swap instead of the legacy function:

```javascript
UniversalTokenRouter.exec([{
    output: 0,
    code: AddressZero,
    data: '0x',
    tokens: [{
        eip: 20,
        token: path[0],
        id: 0,
        offset: 0, // use exact amount specified bellow
        amount: amountIn,
        recipient: UniswapV2Library.pairFor(factory, path[0], path[1]),
    }],
}, {
    output: 1,
    code: UniswapV2Helper01.address,
    data: encodeFunctionData("swapExactTokensForTokens", [amountIn, amountOutMin, path, to, deadline]),
    tokens: [{
        offset: 0, // balance change verification
        eip: 20,
        token: path[path.length-1],
        id: 0,
        amount: amountOutMin,
        recipient: to,
    }],
}])
```

#### `UniswapRouter.swapTokensForExactTokens`

Legacy function:

```solidity
UniswapV2Router01.swapTokensForExactTokens(
    uint amountOut,
    uint amountInMax,
    address[] calldata path,
    address to,
    uint deadline
)
```

This function accepts the `uint[] amounts` as the last `bytes` param, decode and pass to the internal function `_swap` of `UniswapV2Helper01`.

```solidity
UniswapV2Helper01.swap(address[] calldata path, address _to, bytes calldata amountsBytes) external {
    uint[] memory amounts = abi.decode(amountsBytes, (uint[]));
    _swap(amounts, path, _to);
}
```

This transaction is signed by users to execute the swap instead of the legacy function:

```javascript
UniversalTokenRouter.exec([{
    output: 0,
    code: UniswapV2Helper01.address,
    data: encodeFunctionData("getAmountIns", [amountOut, path]),
    tokens: [{
        eip: 20,
        token: path[0],
        id: 0,
        offset: 64, // first item of getAmountIns result array
        amount: amountInMax,
        recipient: UniswapV2Library.pairFor(factory, path[0], path[1]),
    }],
}, {
    output: 1,
    code: UniswapV2Helper01.address,
    data: encodeFunctionData("swap", [path, to, LAST_INPUT_RESULT]),
    tokens: [{
        offset: 0, // balance change verification
        eip: 20,
        token: path[path.length-1],
        id: 0,
        amount: amountOut,
        recipient: to,
    }],
}])
```

The result of input action's `getAmountIns` will replace the `LAST_INPUT_RESULT` bytes, save the transaction from calculating twice with the same data.

#### `UniswapRouter.addLiquidity`

Legacy function:

```solidity
UniswapV2Router01.addLiquidity(
    address tokenA,
    address tokenB,
    uint amountADesired,
    uint amountBDesired,
    uint amountAMin,
    uint amountBMin,
    address to,
    uint deadline
)
```

This transaction is signed by users to execute the swap instead of the legacy function:

```javascript
UniversalTokenRouter.exec([{
    output: 0,
    code: UniswapV2Helper01.address,
    data: encodeFunctionData("_addLiquidity", [
        tokenA,
        tokenB,
        amountADesired,
        amountBDesired,
        amountAMin,
        amountBMin,
    ]),
    tokens: [{
        eip: 20,
        token: tokenA,
        id: 0,
        offset: 32,             // first item of _addLiquidity results
        amount: amountADesired, // amountInMax
        recipient: UniswapV2Library.pairFor(factory, tokenA, tokenB),
    }, {
        eip: 20,
        token: tokenB,
        id: 0,
        offset: 64,             // second item of _addLiquidity results
        amount: amountBDesired, // amountInMax
        recipient: UniswapV2Library.pairFor(factory, tokenA, tokenB),
    }],
}, {
    output: 1,
    code: UniswapV2Library.pairFor(factory, tokenA, tokenB),
    data: encodeFunctionData("mint", [to]),
    tokens: [{
        offset: 0,  // balance change verification
        eip: 20,
        token: UniswapV2Library.pairFor(factory, tokenA, tokenB),
        id: 0,
        amount: 1,  // amountOutMin: just enough to verify the correct recipient
        recipient: to,
    }],
}])
```

The `tokens` verification of the last output action is not performed by Uniswap's legacy function and can be skipped. But the output token verification SHOULD always be done for the `UniversalTokenRouter` so user can see and review the token behavior instead of blindly trust the front-end code.

## Rationale

The `Permit` type signature is not supported since the purpose of the Universal Token Router is to eliminate all `approve` signatures for new tokens, and *most* for old tokens.

Flashloan transactions are out of scope since it requires support from the application contracts themself.

## Backwards Compatibility

### Tokens

Old token contracts (EIP-20, EIP-721 and EIP-1155) require approval for the Universal Token Router once for each account.

New token contracts can pre-configure the Universal Token Router as a trusted spender, and no approval transaction is required.

### Application Contracts

All application contracts that accept `recipient` (or `to`) argument instead of using `msg.sender` as the beneficiary address are compatible with the UTR out of the box.

Application contracts that transfer tokens (EIP-20, EIP-721, and EIP-1155) to `msg.sender` can use the UTR output token transfer sub-action to re-direct tokens to another `recipient` address.

```javascript
// sample code to deposit WETH and transfer them out
UniversalTokenRouter.exec([{
    output: 0,
    code: AddressZero,
    data: '0x',
    tokens: [{
        eip: 0,                 // ETH
        adr: AddressZero,
        id: 0,
        offset: 0,              // use the exact amount specified bellow
        amount: 123,
        recipient: AddressZero, // pass it as the value for the next output action
    }],
}, {
    output: 1,
    code: WETH.address,
    data: encodeFunctionData('deposit', []),    // WETH.deposit returns WETH token to the UTR contract
    tokens: [{
        offset: 1,  // token transfer sub-action
        eip: 20,
        adr: WETH.address,
        id: 0,
        amount: 0,  // entire WETH balance of this UTR contract
        recipient: SomeRecipient,
    }],
}, {
    // ... continue to use WETH in SomeRecipient
}], {value: 123})
```

Applications can also deploy additional adapter contracts to add a `recipient` to their functions.

```solidity
// sample adapter contract for WETH
contract WethAdapter {
    address immutable WETH = 0x....;
    function deposit(address recipient) external payable {
        IWETH(WETH).deposit(){value: msg.value};
        TransferHelper.safeTransfer(WETH, recipient, msg.value);
    }
}
```

Application contracts that use `msg.sender` as the beneficiary address in their internal storage without any function for ownership transfer are incompatible with the UTR.

## Reference Implementation

```solidity
contract UniversalTokenRouter is IUniversalTokenRouter {
    uint constant LAST_INPUT_RESULT = uint(keccak256('UniversalTokenRouter.LAST_INPUT_RESULT'));
    uint constant EIP_721_ALL = uint(keccak256('UniversalTokenRouter.EIP_721_ALL'));

    function exec(
        Action[] calldata actions
    ) override external payable {
    unchecked {
        // track the balances before any action is executed
        uint[][] memory balances = new uint[][](actions.length);
        for (uint i = 0; i < actions.length; ++i) {
            if (actions[i].output == 0 || actions[i].tokens.length == 0) {
                continue;
            }
            balances[i] = new uint[](actions[i].tokens.length);
            for (uint j = 0; j < balances[i].length; ++j) {
                if (actions[i].tokens[j].offset == 0) {
                    balances[i][j] = _balanceOf(actions[i].tokens[j], actions[i].tokens[j].recipient);
                }
            }
        }

        uint value; // track the ETH value to pass to next output action transaction value
        bytes memory lastInputResult;
        for (uint i = 0; i < actions.length; ++i) {
            Action memory action = actions[i];
            if (action.output == 0) {
                // input action
                if (action.data.length > 0) {
                    bool success;
                    (success, lastInputResult) = action.code.call(action.data);
                    if (!success) {
                        assembly {
                            revert(add(lastInputResult,32),mload(lastInputResult))
                        }
                    }
                }
                for (uint j = 0; j < action.tokens.length; ++j) {
                    Token memory token = action.tokens[j];
                    if (token.offset >= 32) {
                        // require(inputParams.length > 0, "UniversalTokenRouter: OFFSET_OF_EMPTY_INPUT");
                        uint amount = _sliceUint(lastInputResult, token.offset);
                        require(amount <= token.amount, "UniversalTokenRouter: EXCESSIVE_INPUT_AMOUNT");
                        token.amount = amount;
                    }
                    if (token.eip == 0 && token.recipient == address(0x0)) {
                        value = token.amount;
                        // ETH not transfered here will be passed to the next output call value
                    } else if (token.amount > 0) {
                        _transferFrom(token, msg.sender);
                    }
                }
            } else {
                // output action
                if (action.data.length > 0) {
                    uint length = action.data.length;
                    if (length >= 4+32*3 &&
                        _sliceUint(action.data, length) == LAST_INPUT_RESULT &&
                        _sliceUint(action.data, length-32) == 32)
                    {
                        action.data = _concat(action.data, length-32, lastInputResult);
                    }
                    (bool success, bytes memory result) = action.code.call{value: value}(action.data);
                    // ignore output action error if output == 2
                    if (!success && action.output == 2) {
                        assembly {
                            revert(add(result,32),mload(result))
                        }
                    }
                    delete value; // clear the ETH value after transfer
                }
                for (uint j = 0; j < action.tokens.length; ++j) {
                    Token memory token = actions[i].tokens[j];
                    if (token.offset > 0) {
                        // token transfer sub-action
                        if (token.offset >= 32) {
                            token.amount = _sliceUint(lastInputResult, token.offset);
                        } else if (token.amount == 0) {
                            token.amount = _balanceOf(token, address(this));
                        }
                        _transferFrom(token, address(this));
                    } else {
                        // verify the balance change
                        uint balance = _balanceOf(token, token.recipient);
                        uint change = balance - balances[i][j]; // overflow checked with `change <= balance` bellow
                        require(change >= token.amount && change <= balance, 'UniversalTokenRouter: INSUFFICIENT_OUTPUT_AMOUNT');
                    }
                }
            }
        }

        // refund any left-over ETH
        uint leftOver = address(this).balance;
        if (leftOver > 0) {
            TransferHelper.safeTransferETH(msg.sender, leftOver);
        }
    } }

    function _transferFrom(Token memory token, address from) internal {
    unchecked {
        if (token.eip == 20) {
            if (from == address(this)) {
                TransferHelper.safeTransfer(token.adr, token.recipient, token.amount);
            } else {
                TransferHelper.safeTransferFrom(token.adr, from, token.recipient, token.amount);
            }
        } else if (token.eip == 1155) {
            IERC1155(token.adr).safeTransferFrom(from, token.recipient, token.id, token.amount, "");
        } else if (token.eip == 721) {
            IERC721(token.adr).safeTransferFrom(from, token.recipient, token.id);
        } else if (token.eip == 0) {
            TransferHelper.safeTransferETH(token.recipient, token.amount);
        } else {
            revert("UniversalTokenRouter: INVALID_EIP");
        }
    } }

    function _balanceOf(Token memory token, address owner) internal view returns (uint balance) {
    unchecked {
        if (token.eip == 20) {
            return IERC20(token.adr).balanceOf(owner);
        }
        if (token.eip == 1155) {
            return IERC1155(token.adr).balanceOf(owner, token.id);
        }
        if (token.eip == 721) {
            if (token.id == EIP_721_ALL) {
                return IERC721(token.adr).balanceOf(owner);
            }
            return IERC721(token.adr).ownerOf(token.id) == owner ? 1 : 0;
        }
        if (token.eip == 0) {
            return owner.balance;
        }
        revert("UniversalTokenRouter: INVALID_EIP");
    } }

    function _sliceUint(bytes memory bs, uint start) internal pure returns (uint x) {
    unchecked {
        // require(bs.length >= start + 32, "slicing out of range");
        assembly {
            x := mload(add(bs, start))
        }
    } }

    /// https://github.com/GNSPS/solidity-bytes-utils/blob/master/contracts/BytesLib.sol
    /// @param length length of the first preBytes
    function _concat(
        bytes memory preBytes,
        uint length,
        bytes memory postBytes
    ) internal pure returns (bytes memory bothBytes) {
        assembly {
            // Get a location of some free memory and store it in bothBytes as
            // Solidity does for memory variables.
            bothBytes := mload(0x40)

            // Store the length of the first bytes array at the beginning of
            // the memory for bothBytes.
            mstore(bothBytes, length)

            // Maintain a memory counter for the current write location in the
            // temp bytes array by adding the 32 bytes for the array length to
            // the starting location.
            let mc := add(bothBytes, 0x20)
            // Stop copying when the memory counter reaches the length of the
            // first bytes array.
            let end := add(mc, length)

            for {
                // Initialize a copy counter to the start of the preBytes data,
                // 32 bytes into its memory.
                let cc := add(preBytes, 0x20)
            } lt(mc, end) {
                // Increase both counters by 32 bytes each iteration.
                mc := add(mc, 0x20)
                cc := add(cc, 0x20)
            } {
                // Write the preBytes data into the bothBytes memory 32 bytes
                // at a time.
                mstore(mc, mload(cc))
            }

            // Add the length of postBytes to the current length of bothBytes
            // and store it as the new length in the first 32 bytes of the
            // bothBytes memory.
            length := mload(postBytes)
            mstore(bothBytes, add(length, mload(bothBytes)))

            // Move the memory counter back from a multiple of 0x20 to the
            // actual end of the preBytes data.
            mc := sub(end, 0x20)
            // Stop copying when the memory counter reaches the new combined
            // length of the arrays.
            end := add(end, length)

            for {
                let cc := postBytes
            } lt(mc, end) {
                mc := add(mc, 0x20)
                cc := add(cc, 0x20)
            } {
                mstore(mc, mload(cc))
            }

            // Update the free-memory pointer by padding our last write location
            // to 32 bytes: add 31 bytes to the end of bothBytes to move to the
            // next 32 byte block, then round down to the nearest multiple of
            // 32. If the sum of the length of the two arrays is zero then add
            // one before rounding down to leave a blank 32 bytes (the length block with 0).
            mstore(0x40, and(
              add(add(end, iszero(add(length, mload(preBytes)))), 31),
              not(31) // Round down to the nearest 32 bytes.
            ))
        }
    }
}
```

## Security Considerations

`LAST_INPUT_RESULT` SHOULD only be used in output action for gas optimization, not as trusted conditions. Application contract code MUST always expect arbitruary, malformed or mallicious `bytes` data can be passed in where the `LAST_INPUT_RESULT` is expected.

## Copyright

Copyright and related rights waived via [CC0](../LICENSE.md).
